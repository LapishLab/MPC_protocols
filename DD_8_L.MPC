\DD_8_L

\Inputs
^Dresp = 1
^Iresp = 3

\Outputs
^Hlight = 7
^Sync = 10
^Tone = 6
^Pellet = 3
^Dlever = 1
^Ilever = 2
^Dlight = 4
^Ilight = 5

\Variables
\A (initial lever response)
\B (delay response count during choice)
\C (delay response count during force)
\D (session delay & consecutive response counters)
\E (initial lever latency)
\F (choice lever latency)
\G (force lever latency)
\H (type of trial (1=noinit, 2=nochoice, 3=dchoice, 4=ichoice, 5=nodforce, 6=noiforce, 7=dforce, 8=iforce)
\I (inter-trial interval)
\J (immediate response count during choice)
\K (trial counter)
\L (immediate response count during force)
\M (missed choice (no response))
\N (time at end of trial)
\O (immediate lever reinforcer value)
\P (pellet loop, immediate lever adjustment, total pellet count)
\Q (session condition (1=init, 2=ch, 3=dch, 4=ich, 5=dforce, 6=iforce, 7=dforceresp, 8=iforceresp))
\R (reinforcer counts)
\S (elements of arrays U,V,W,X,Y,Z)
\T (timer & initial/choice latencies)
\U (delay response during initial lever)
\V (delay response during choice)
\W (delay response during forced delay)
\X (immediate response during initial lever)
\Y (immediate response during choice)
\Z (immediate response during forced immediate)


\Z-Pulses
\Z1 (restart)
\Z2 (move to choice lever)
\Z3 (P(1) pellets immediate)
\Z4 (6 pellets delay)
\Z5 (calculate ITI & session test)
\Z6 (assess force (immediate vs delay))
\Z7 (force (delay))
\Z8 (force (immediate))

DIM A = 3599    \(filled with initial lever responses (0=none, 1=delay, 2=init))
DIM B = 3599    \(filled with choice delay response counts)
DIM C = 3599    \(filled with force delay response counts)
DIM D = 2    \(0=session delay(0,1,2,4,8), 1-2=consecutive resp count(delay/immediate))
DIM E = 3599    \(filled with initial lever latencies)
DIM F = 3599    \(filled with choice lever latencies)
DIM G = 3599    \(filled with force lever latencies)
DIM H = 3599    \(filled with type of trial)
DIM J = 3599    \(filled with choice immediate response counts)
DIM L = 3599    \(filled with force immediate response counts)
DIM N = 3599    \(filled with trial-ending times)
DIM O = 3599    \(filled with immediate lever reinforcer values)
DIM P = 2       \(0=number of pellets, 1=immediate lever value, 2=total pellet count)
DIM R = 3       \(0=dchoice, 1=ichoice, 2=dforce, 3=iforce)
DIM S = 5       \(array element counter 0=U, 1=V, 2=W, 3=X, 4=Y, 5=Z)
DIM T = 7       \(0=timer, 1-3=initial(out/resp/late), 4-6=choice(out/resp/late), 7=end session)
DIM U = 3599    \(filled with initial delay responses)
DIM V = 3599    \(filled with choice delay responses)
DIM W = 3599    \(filled with forced delay responses)
DIM X = 3599    \(filled with initial immediate responses)
DIM Y = 3599    \(filled with choice immediate responses)
DIM Z = 3599    \(filled with forced immediate responses)
DISKCOLUMNS = 1

s.s.1,    \1" timer
s1,
.1": SET T(0)=T(0)+.1 ---> s1

s.s.2,    \Initialize & set variables
s1,
#START: SET D(0)=8", I=0, P(1)=3; SHOW 1,Trial,K; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); Z1 ---> s2
s2,
35': SET T(7)=1; SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)) ---> s1

s.s.3,    \Initial lever
s1,
#Z1: OFF^Hlight, ^Sync, ^Dlight, ^Ilight, ^Dlever, ^Ilever; SET Q=1, O(K)=P(1), E(K)=0, F(K)=0, G(K)=0 ---> s2
s2,
.01": IF T(7)=1 [@End,@Continue]
    @End: ---> s5
    @Continue: ON^Hlight, ^Sync ---> s3
s3,
10": OFF^Sync; ON^Dlever, ^Ilever; SET T(1)=T(0) ---> s4
s4,
#R^Dresp: OFF^Hlight, ^Dlever, ^Ilever; SET T(2)=T(0), A(K)=1, T(3)=T(2)-T(1), E(K)=T(3); Z2 ---> s1
#R^Iresp: OFF^Hlight, ^Dlever, ^Ilever; SET T(2)=T(0), A(K)=2, T(3)=T(2)-T(1), E(K)=T(3); Z2 ---> s1
10": SET H(K)=Q, N(K)=T(0), A(K)=0, E(K)=10; SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K; Z1 ---> s1
s5,
.01": SET H(K)=-987.987, N(K)=-987.987, B(K)=-987.987, C(K)=-987.987, J(K)=-987.987, L(K)=-987.987, O(K)=-987.987, A(K)=-987.987, E(K)=-987.987, F(K)=-987.987, G(K)=-987.987 ---> STOPABORTFLUSH

s.s.4,    \Choice lever
s1,
#Z2: SET M=24-T(3), M=M" ---> s2
s2,
.01": IF (D(1)<4) AND (D(2)<4) [@Choice,@Force]
    @Choice: ---> s3
    @Forced: Z6 ---> s1
s3,
1": ON^Dlight, ^Ilight, ^Dlever, ^Ilever; SET T(4)=T(0), Q=2 ---> s4
s4,
#R^Dresp:  OFF^Ilight; SET T(5)=T(0), D(2)=0, T(6)=T(5)-T(4), F(K)=T(6); Z4 ---> s5
#R^Iresp: OFF^Dlight; SET T(5)=T(0), D(1)=0, T(6)=T(5)-T(4), F(K)=T(6); Z3 ---> s5
M#T: SET H(K)=Q, N(K)=T(0), F(K)=M/100; SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K; Z1 ---> s1
s5,
.1": ---> s1

s.s.5,    \Immediate lever P(1) pellets
s1,
#Z3: SET P(0)=0, Q=4 ---> s2
s2,
.01": IF P(0)=P(1) [@Yes,@No]
    @Yes: ---> s4
    @No: ON^Pellet; ADD P(0) ---> s3
s3,
.25": OFF^Pellet ---> s2
s4,
.01": ADD R(1), D(2); SET P(2)=P(2)+P(1); LIMIT P(1),-1,0; Z5 ---> s1

s.s.6,    \Delay lever 6 pellets
s1,
#Z4: SET P(0)=0, Q=3 ---> s2
s2,
D(0)#T ---> s3
s3,
.01": IF P(0)=6 [@Yes,@No]
    @Yes: ---> s5
    @No: ON^Pellet; ADD P(0) ---> s4
s4,
.25": OFF^Pellet ---> s3
s5,
.01": ADD R(0), D(1); SET P(2)=P(2)+6; LIMIT P(1),1,6; Z5 ---> s1

s.s.7,    \Show timers & end session check
s1,
#Z5: SET I=(24-(T(3)+T(6))), I=I" ---> s2
s2,
.01": IF (R(0)+R(1))=30 [@Yes,@No]
    @Yes: SET H(K)=Q, N(K)=T(0); SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K ---> s3
    @No: ---> s4
s3,
.01": SET H(K)=-987.987, N(K)=-987.987, B(K)=-987.987, C(K)=-987.987, J(K)=-987.987, L(K)=-987.987, O(K)=-987.987, A(K)=-987.987, E(K)=-987.987, F(K)=-987.987, G(K)=-987.987 ---> STOPABORTFLUSH
s4,
I#T: SET H(K)=Q, N(K)=T(0); SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K; Z1 ---> s1

s.s.8,    \Assess force (delay vs immediate)
s1,
#Z6: IF D(2)=8 [@Delay,@Immed]
    @Delay: Z7 ---> s1
    @Immed: Z8 ---> s1

s.s.9,    \Force (delay) 6 pellets
s1,
#Z7: SET Q=5 ---> s2
s2,
1": ON^Dlight, ^Dlever; SET T(4)=T(0) ---> s3
s3,
#R^Dresp: SET P(0)=0, Q=7, T(5)=T(0), T(6)=T(5)-T(4), G(K)=T(6) ---> s4
M#T: SET H(K)=Q, N(K)=T(0), G(K)=M/100; SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K; Z1 ---> s1
s4,
.1":  ---> s5
s5,
D(0)#T: ---> s6
s6,
.01": IF P(0)=6 [@Yes,@No]
    @Yes: ---> s8
    @No: ON^Pellet; ADD P(0) ---> s7
s7,
.25": OFF^Pellet ---> s6
s8,
.01": ADD R(2); SET D(1)=0, D(2)=0, P(2)=P(2)+6; Z5 ---> s1

s.s.10,    \Force (immediate) P(1) pellets
s1,
#Z8: SET Q=6 ---> s2
s2,
1": ON^Ilight, ^Ilever; SET T(4)=T(0) ---> s3
s3,
#R^Iresp: SET P(0)=0, Q=8, T(5)=T(0), T(6)=T(5)-T(4), G(K)=T(6) ---> s4
M#T: SET H(K)=Q, N(K)=T(0), G(K)=M/100; SHOW 1,Trial,K+1; SHOW 2,Trial Type,H(K); SHOW 3,Choice Count,(R(0)+R(1)); SHOW 4,DReinf,(R(0)+R(2)); SHOW 5,IReinf,(R(1)+R(3)); ADD K; Z1 ---> s1
s4,
.1":  ---> s5
s5,
.01": IF P(0)=P(1) [@Yes,@No]
    @Yes: ---> s7
    @No: ON^Pellet; ADD P(0) ---> s6
s6,
.25": OFF^Pellet ---> s5
s7,
.01": ADD R(3); SET D(1)=0, D(2)=0, P(2)=P(2)+P(1); Z5 ---> s1

s.s.11,        \For Dresp - determine conditions (inital,choice,force) and record time & response counts
s1,
#R^Dresp: IF Q=1 [@Yes,@No]
    @Yes: SET U(S(0))=T(0); ADD S(0) ---> s2
    @No: IF (Q>=2) AND (Q<=4) [@Yes,@No]
        @Yes: SET V(S(1))=T(0); ADD S(1), B(K) ---> s2
        @No: IF (Q=5) OR (Q=7) [@Yes,@No]
            @Yes: SET W(S(2))=T(0); ADD S(2), C(K) ---> s2
            @No: ---> s2
s2,
.01": SET U(S(0))=-987.987, V(S(1))=-987.987, W(S(2))=-987.987 ---> s1

s.s.12,        \For Iresp - determine conditions (inital,choice,force) and record time & response counts
s1,
#R^Iresp: IF Q=1 [@Yes,@No]
    @Yes: SET X(S(3))=T(0); ADD S(3) ---> s2
    @No: IF (Q>=2) AND (Q<=4) [@Yes,@No]
        @Yes: SET Y(S(4))=T(0); ADD S(4), J(K) ---> s2
        @No: IF (Q=6) OR (Q=8) [@Yes,@No]
            @Yes: SET Z(S(5))=T(0); ADD S(5), L(K) ---> s2
            @No: ---> s2
s2,
.01": SET X(S(3))=-987.987, Y(S(4))=-987.987, Z(S(5))=-987.987 ---> s1
